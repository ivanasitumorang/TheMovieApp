plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs'
    id 'de.mannodermaus.android-junit5'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.azuka.themovieapp"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets.each {
        it.java.srcDirs += "src/$it.name/kotlin"
    }

    testOptions {
        junitPlatform {
            filters {
                engines {
                    include 'spek2'
                }
            }
            jacocoOptions {
                // here goes all jacoco config, for example
                html.enabled = true
                xml.enabled = false
                csv.enabled = false
            }
        }
        unitTests.all {
            testLogging.events = ["passed", "skipped", "failed"]
        }
        unitTests.returnDefaultValues = true
    }

    buildTypes {
        debug {
            minifyEnabled false
            buildConfigField "String", "TMDB_IMAGE_URL", "\"https://image.tmdb.org/t/p/\""
        }

        release {
            minifyEnabled true
            buildConfigField "String", "TMDB_IMAGE_URL", "\"https://image.tmdb.org/t/p/\""
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    androidExtensions {
        experimental = true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    //coroutine
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"

    implementation "androidx.recyclerview:recyclerview:1.1.0"

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"

    //navigation arch
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.1"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.1"

    implementation 'com.google.code.gson:gson:2.8.6'

    //by viewmodels()
    implementation "androidx.activity:activity-ktx:1.1.0"
    implementation "androidx.fragment:fragment-ktx:1.3.0-beta01"

    // Picasso
    implementation "com.squareup.picasso:picasso:2.71828"

    debugImplementation "androidx.test:core:1.3.0"
    debugImplementation "androidx.fragment:fragment-testing:1.3.0-beta01"


    /**
     * Test Implementation
     */

    // spek
    testImplementation "org.spekframework.spek2:spek-dsl-jvm:2.0.11"
    testImplementation "org.spekframework.spek2:spek-runner-junit5:2.0.11"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.7.0"

    testImplementation "org.mockito:mockito-core:2.24.5"
    testImplementation 'junit:junit:4.13.1'


    /**
     * Android Test Implementation
     */

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.3.0"
    androidTestImplementation "androidx.test:rules:1.3.0"
    androidTestImplementation "androidx.navigation:navigation-testing:2.3.1"
    androidTestImplementation 'org.mockito:mockito-android:2.24.5'
}